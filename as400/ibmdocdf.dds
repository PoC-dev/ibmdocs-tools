     A* Copyright 2023 Patrik Schindler <poc@pocnet.net>.
     A*
     A* This file is part of the IBM Documentation Utilities, to be found on
     A* https://github.com/PoC-dev/ibmdocs-tools
     A* see there for further details.
     A*
     A* This is free software; you can redistribute it and/or modify it
     A* under the terms of the GNU General Public License as published by the
     A* Free Software Foundation; either version 2 of the License, or (at your
     A* option) any later version.
     A*
     A* It is distributed in the hope that it will be useful, but WITHOUT
     A* ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
     A* FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License
     A* for more details.
     A*
     A* You should have received a copy of the GNU General Public License along
     A* with it; if not, write to the Free Software Foundation, Inc., 59
     A* Temple Place, Suite 330, Boston, MA 02111-1307 USA or get it at
     A* http://www.gnu.org/licenses/gpl.html
     A*
     A* Globals ----------------------------------------------------------------
     A                                      REF(IBMDOCPF)
     A                                      ERRSFL
     A                                      HELP
     A                                      ALTHELP(CA01)
     A                                      HLPPNLGRP(GLOBAL IBMDOCHP)
     A* Subfile Screen ---------------------------------------------------------
     A          R MAINSFL                   SFL
     A  99                                  SFLNXTCHG
     A*         ##What may we do with the selected record?
     A            OPT            1A  B  9  3VALUES(' ' '2' '3' '4' '5')
     A  99                                  DSPATR(RI)
     A*         ##Pull values from the table definition itself...
     A            DOCNBR    R        O  9  7
     A            TITLE$        41A  O  9 29
     A            RELEASED  R        O  9 72EDTCDE(3)
     A            DOCTYPE$       2A  O  9 78
     A* Subfile Control Screen -------------------------------------------------
     A          R MAINCTL                   SFLCTL(MAINSFL)
     A                                      CHGINPDFT(LC UL)
     A*                                   ##Command Key definitions.
     A                                      CA03(03 'Verlassen')
     A                                      CA05(05 'Aktualisieren')
     A                                      CA06(06 'Neu')
     A                                      CA11(11 'Sortierung')
     A                                      CA12(12 'Abbrechen')
     A N34                                  PAGEDOWN(25)
     A N35                                  PAGEUP(26)
     A                                      HOME(27 'Suchfeld')
     A                                      VLDCMDKEY(29 'Any valid key')
     A*                                   ##Basic Settings for subfile ctrl.
     A                                      OVERLAY
     A  91                                  SFLMSGID(ERR0012 GENERICMSG 91)
     A  92                                  SFLMSGID(ERR1021 GENERICMSG 92)
     A  93                                  SFLMSGID(ERR1218 GENERICMSG 93)
     A  95                                  SFLMSGID(INF0001 GENERICMSG 95)
     A                                      SFLSIZ(&SFLSIZ)
     A                                      SFLPAG(13)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('Dokumentenverwaltung - -
     A                                      Listendarstellung')
     A          H                           HLPARA(3 1 3 80)
     A                                      HLPPNLGRP('MAIN/POSTO' IBMDOCHP)
     A          H                           HLPARA(5 2 6 48)
     A                                      HLPPNLGRP('MAIN/SFLOPTS' IBMDOCHP)
     A          H                           HLPARA(6 67 6 79)
     A                                      HLPPNLGRP('MAIN/COUNT' IBMDOCHP)
     A          H                           HLPARA(*CNST 1)
     A                                      HLPPNLGRP('SFLHDR/OPT' IBMDOCHP)
     A          H                           HLPARA(*CNST 2)
     A                                      HLPPNLGRP('SFLHDR/DOCNBR' IBMDOCHP)
     A          H                           HLPARA(*CNST 3)
     A                                      HLPPNLGRP('SFLHDR/TITLE' IBMDOCHP)
     A          H                           HLPARA(*CNST 4)
     A                                      HLPPNLGRP('SFLHDR/RELEASED'
     A                                      IBMDOCHP)
     A          H                           HLPARA(*CNST 5)
     A                                      HLPPNLGRP('SFLHDR/DOCTYP' IBMDOCHP)
     A            SFLRCDNBR      3S 0H      SFLRCDNBR(CURSOR)
     A            SFLSIZ         5S 0P
     A*         ##End of definitions. Here be real screen elements.
     A                                  1 30'Dokumentenverwaltung'
     A                                      DSPATR(HI)
     A                                  3  7'Listenanfang bei'
     A            POSTO         33A  I  3 25
     A  41                                  DSPATR(PC)
     A  50                              3 62'(Dokumentnummer)'
     A  51                              3 62'(Titel)'
     A  52                              3 62'(Jahr)'
     A                                  5  2'Auswahl eingeben und -
     A                                      Eingabetaste drücken.'
     A                                      COLOR(BLU)
     A                                  6 67'Gesamt'
     A            DBRCDCNT$      5Y 0   6 74EDTCDE(1)
     A                                  6  3'2=Ändern  3=Duplizieren  -
     A                                      4=Löschen  5=Anzeigen'
     A                                      COLOR(BLU)
     A  35                              7 70'Beginn ...' DSPATR(HI)
     A                                  8  2'Opt' DSPATR(HI) HLPID(1)
     A                                  8  7'Dokumentnummer' DSPATR(HI) HLPID(2)
     A  50                                  DSPATR(UL)
     A                                  8 29'Titel' DSPATR(HI) HLPID(3)
     A  51                                  DSPATR(UL)
     A                                  8 72'Jahr' DSPATR(HI) HLPID(4)
     A  52                                  DSPATR(UL)
     A                                  8 78'FT' DSPATR(HI) HLPID(5)
     A* Footer of the form -----------------------------------------------------
     A* A subfile control record may contain elements above or below the actual
     A* subfile, but not both.
     A          R MAINBTM
     A                                      HLPTITLE('Dokumentenverwaltung - -
     A                                      Listendarstellung')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/SFLBTM' IBMDOCHP)
     A                                 23  2'F1=Hilfe   -
     A                                      F3=Verlass.   -
     A                                      F5=Aktual.   -
     A                                      F6=Neu   -
     A                                      F11=Sortierung   -
     A                                      F12=Abbrechen'
     A                                      COLOR(BLU)
     A* No data instead of SFL -------------------------------------------------
     A          R MAINND                    OVERLAY
     A                                      HLPTITLE('Dokumentenverwaltung - -
     A                                      Listendarstellung')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/SFLND' IBMDOCHP)
     A                                 10  7'(Keine Datensätze zum Anzeigen.)'
     A* ------------------------------------------------------------------------
     A* Subfile Screen: Delete confirmation ------------------------------------
     A          R DLTSFL                    SFL
     A                                      SFLNXTCHG
     A            DOPT           1A  B  7  3VALUES(' ' '4')
     A  99                                  DSPATR(RI)
     A*         ##Pull values from the table definition itself...
     A            DOCNBR    R        O  7  7
     A            TITLE$    R        O  7 29REFFLD(MAINSFL/TITLE$ *SRC)
     A            RELEASED  R        O  7 72EDTCDE(3)
     A            DOCTYPE$       2A  O  7 78
     A* Subfile Control Screen -------------------------------------------------
     A          R DLTCTL                    SFLCTL(DLTSFL)
     A*                                   ##Command Key definitions.
     A                                      CA12(12 'Abbrechen')
     A                                      VLDCMDKEY(29 'Any valid key')
     A*                                   ##Basic Settings for subfile ctrl.
     A                                      OVERLAY
     A  91                                  SFLMSGID(ERR0012 GENERICMSG 91)
     A  93                                  SFLMSGID(ERR1218 GENERICMSG 93)
     A  96                                  SFLMSGID(INF0999 GENERICMSG 96)
     A                                      SFLSIZ(30)
     A                                      SFLPAG(15)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('Dokumentenverwaltung - -
     A                                      Löschabfrage')
     A          H                           HLPARA(3 1 4 80)
     A                                      HLPPNLGRP('DELETE/SFLOPTS' SFLDLTHP)
     A          H                           HLPARA(*CNST 1)
     A                                      HLPPNLGRP('DELETE/OPT' SFLDLTHP)
     A          H                           HLPARA(6 7 6 80)
     A                                      HLPPNLGRP('DELETE/FIELDS' SFLDLTHP)
     A          H                           HLPARA(7 1 21 80)
     A                                      HLPPNLGRP('DELETE/SFLDTA' SFLDLTHP)
     A            SFLDLTNBR      3S 0H      SFLRCDNBR(CURSOR)
     A*         ##End of definitions. Here be real screen elements.
     A                                  1 29'Löschen von Einträgen'
     A                                      DSPATR(HI)
     A                                  3  2'Auswahl ggfs. anpassen und -
     A                                      Eingabetaste drücken.'
     A                                      COLOR(BLU)
     A                                  4  3'4=Löschen  Leer=Nicht löschen'
     A                                      COLOR(BLU)
     A                                  6  2'Opt' DSPATR(HI) HLPID(1)
     A                                  6  7'Dokument' DSPATR(HI)
     A                                  6 29'Titel' DSPATR(HI)
     A                                  6 72'Jahr' DSPATR(HI)
     A                                  6 78'FT' DSPATR(HI)
     A* Footer of the form -----------------------------------------------------
     A* A subfile control record may contain elements above or below the actual
     A* subfile, but not both.
     A          R DLTBTM
     A                                      HLPTITLE('Dokumentenverwaltung - -
     A                                      Löschabfrage')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('DELETE/SFLBTM' SFLDLTHP)
     A                                 23  2'F1=Hilfe   F12=Abbrechen'
     A                                      COLOR(BLU)
     A* No data instead of SFL -------------------------------------------------
     A          R DLTND                     OVERLAY
     A                                      HLPTITLE('Dokumentenverwaltung - -
     A                                      Löschabfrage')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/SFLND' IBMDOCHP)
     A                                  8  7'(Keine Datensätze zum Anzeigen.)'
     A* ------------------------------------------------------------------------
     A* One form for displaying further details, or editing existing, or adding
     A* new records. We can switch this form to "looks like output only fields"
     A* with IN44.
     A          R DETAILFRM
     A                                      CHGINPDFT(LC UL)
     A                                      CHANGE(28 'Content has changed')
     A                                      CA03(03 'Verlassen')
     A                                      CA12(12 'Abbrechen')
     A                                      RTNCSRLOC(*RECNAME &CSREC &CSFLD)
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('Dokumentenverwaltung - -
     A                                      Details')
     A          H                           HLPARA(1 1 1 80)
     A                                      HLPPNLGRP('DETAIL/MODE' IBMDDTLHP)
     A          H                           HLPARA(3 1 3 80)
     A                                      HLPPNLGRP('DETAIL/OPTIONS'
     A                                      IBMDDTLHP)
     A          H                           HLPARA(5 1 5 38)
     A                                      HLPPNLGRP('DFIELD/DOCNBR' IBMDDTLHP)
     A          H                           HLPARA(7 1 10 80)
     A                                      HLPPNLGRP('DFIELD/TITLE' IBMDDTLHP)
     A          H                           HLPARA(12 1 12 24)
     A                                      HLPPNLGRP('DFIELD/RELEASED'
     A                                      IBMDDTLHP)
     A          H                           HLPARA(14 1 17 80)
     A                                      HLPPNLGRP('DFIELD/SUBTITLE'
     A                                      IBMDDTLHP)
     A*         ##End of definitions. Here be real screen elements.
     A  42N43                           1 30'Neuen Eintrag erfassen'
     A                                      DSPATR(HI)
     A N42 43                           1 32'Eintrag ändern'
     A                                      DSPATR(HI)
     A N42N43                           1 31'Eintrag anzeigen'
     A                                      DSPATR(HI)
     A  42N43                           3  2'Felder ausfüllen und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A N42 43                           3  2'Felder anpassen und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A N42N43                           3  2'Details:'
     A                                      COLOR(BLU)
     A                                  5  3'Dokumentnummer'
     A            DOCNBR    R        B  5 21
     A  44                                  DSPATR(PR)
     A N44                                  DSPATR(UL)
     A N44 60                               DSPATR(PC)
     A  92                                  ERRMSGID(ERR1021 GENERICMSG 92)
     A                                  7  3'Titel  . . . . .'
     A            TITLE     R        B  7 21CNTFLD(60)
     A  44                                  DSPATR(PR)
     A N44                                  DSPATR(UL)
     A N44 61                               DSPATR(PC)
     A                                 12  3'Erschienen . . .'
     A            RELEASED  R     Y  B 12 21EDTCDE(3)
     A  44                                  DSPATR(PR)
     A N44                                  DSPATR(UL)
     A N44 62                               DSPATR(PC)
     A                                 14  3'Subtitel . . . .'
     A            SUBTITLE  R        B 14 21CNTFLD(60)
     A  44                                  DSPATR(PR)
     A N44                                  DSPATR(UL)
     A N44 63                               DSPATR(PC)
     A*
     A* This is entirely to prevent DSPATR(RI) error indication with just a
     A*  warning message. Place in least used screen position.
     A            DUMMY$$        1A  O 24 80DSPATR(ND)
     A  91                                  ERRMSGID(ERR0012 GENERICMSG 91)
     A  93                                  ERRMSGID(ERR1218 GENERICMSG 93)
     A  94                                  ERRMSGID(RDO1218 GENERICMSG 94)
     A  95                                  ERRMSGID(INF0001 GENERICMSG 95)
     A            CSREC         10A  H
     A            CSFLD         10A  H
     A* ------------------------------------------------------------------------
     A* Subfile Screen: Format Information -------------------------------------
     A* FIXME: Currently not in use.
     A          R DETAILSFL                 SFL
     A*         ##Pull values from the table definition itself...
     A            DOCTYPE#       4A  O 19  3
     A            DOTSFIELD     11A  O 19  8
     A  38                                  DSPATR(ND)
     A            DATE_ADDEDR     D  O 19 21REFFLD(DATE_ADDED IBMDOCTYPF)
     A  38                                  DSPATR(ND)
     A            DLSNAME   R        O 19 43REFFLD(DLSNAME IBMDOCTYPF)
     A  38                                  DSPATR(ND)
     A* Subfile Control Screen -------------------------------------------------
     A* One form for displaying further details, or editing existing, or adding
     A* new records. We can switch this form to "looks like output only fields"
     A* with IN44.
     A          R DETAILCTL                 SFLCTL(DETAILSFL)
     A*                                   ##Command Key definitions.
     A                                      CA03(03 'Verlassen')
     A                                      CA12(12 'Abbrechen')
     A                                      VLDCMDKEY(29 'Any valid key')
     A*                                   ##Basic Settings for subfile ctrl.
     A                                      OVERLAY
     A                                      SFLSIZ(4)
     A                                      SFLPAG(3)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('Dokumentenverwaltung - -
     A                                      Details')
     A          H                           HLPARA(*CNST 1)
     A                                      HLPPNLGRP('DFIELD/DOCTYP' IBMDOCHP)
     A          H                           HLPARA(*CNST 2)
     A                                      HLPPNLGRP('DFIELD/DATE_ADDED'
     A                                      IBMDOCHP)
     A          H                           HLPARA(*CNST 3)
     A                                      HLPPNLGRP('DFIELD/DLSNAME' IBMDOCHP)
     A*
     A            DETAILNBR      3S 0H      SFLRCDNBR
     A*         ##End of definitions. Here be real screen elements.
     A                                 18  3'Format' DSPATR(HI) HLPID(1)
     A                                 18 21'Hinzugefügt' DSPATR(HI) HLPID(2)
     A                                 18 43'Kurzname' DSPATR(HI) HLPID(3)
     A* Footer of the form -----------------------------------------------------
     A* A subfile control record may contain elements above or below the actual
     A* subfile, but not both.
     A          R DETAILBTM
     A                                      HLPTITLE('Dokumentenverwaltung - -
     A                                      Details')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/SFLBTM' IBMDOCHP)
     A                                 23  2'F1=Hilfe   F3=Verlassen   -
     A                                      F12=Abbrechen'
     A                                      COLOR(BLU)
     A* No data instead of SFL -------------------------------------------------
     A          R DETAILND                  OVERLAY
     A                                      HLPTITLE('Dokumentenverwaltung - -
     A                                      Details')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/SFLND' IBMDOCHP)
     A                                 19  3'(Keine Formate vorhanden.)'
     A* For vim ----------------------------------------------------------------
     A* vim: syntax=dds colorcolumn=81 autoindent noignorecase
